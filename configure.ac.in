dnl configure.ac
dnl
dnl xfce4-mount-plugin - Mount plugin for xfce4-panel
dnl
dnl 2003-2004 Benedikt Meurer <benny@xfce.org>
dnl 2005-2020 Fabian Nowak <timystery@arcor.de>
dnl 2020 Olaf Hering <olaf@aepfle.de>
dnl 2012 Landry Breuil <landry@xfce.org>
dnl
dnl for Xfce4-panel 4.12 and higher only!

m4_define([mount_version_major], [1])
m4_define([mount_version_minor], [1])
m4_define([mount_version_micro], [5])
m4_define([mount_version_tag], [git]) # Leave empty only for releases, but use 'git' in most cases
m4_define([mount_version_build], [@REVISION@])
m4_define([mount_version], [mount_version_major().mount_version_minor().mount_version_micro()ifelse(mount_version_tag(), [], [], [mount_version_tag()-mount_version_build()])])
m4_define([mount_debug_default], [ifelse(mount_version_tag(), [git], [yes], [minimum])])

AC_COPYRIGHT([Copyright (c) 2006-2020
        The Xfce development team. All rights reserved.])

AC_INIT([xfce4-mount-plugin], [mount_version],
  [https://gitlab.xfce.org/panel-plugins/xfce4-mount-plugin/], [xfce4-mount-plugin])

AM_INIT_AUTOMAKE([1.8 dist-bzip2 tar-ustar no-dist-gzip])
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_MACRO_DIRS([m4])

AM_MAINTAINER_MODE
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

dnl Check for UNIX variants
dnl AC_AIX
dnl AC_ISC_POSIX
dnl AC_MINIX
AC_USE_SYSTEM_EXTENSIONS

dnl Check for basic programs
AC_PROG_CC
AC_PROG_INSTALL
IT_PROG_INTLTOOL([0.35.0])

dnl Initialize libtool
LT_PREREQ([2.2.6])
LT_INIT([disable-static])

dnl No more try substituting "OS" in Makefiles, but might be required in future again
dnl AC_SUBST(OS)

dnl getmentent() requires stdio.h/mntent.h 
AC_CHECK_FUNCS([getmntent])
dnl On BSD, getmntinfo is used instead
AC_CHECK_FUNCS([getmntinfo])

dnl param.h is part of libc6 on Linux, but important for old-style Unix and especially mount.h with getmntinfo, so better keep it for now.
AC_CHECK_HEADERS([sys/param.h])
AC_CHECK_HEADERS([sys/mount.h])

dnl Add -traditional to output variable CC if using the GNU C compiler and ioctl does not work properly without -traditional. That usually happens when the fixed header files have not been installed on an old system. Leave here commented out to comment in if some older *NIX systems might require it as was recently written on the ML.
dnl AC_PROG_GCC_TRADITIONAL

dnl Define size_t to a suitable type, if standard headers do not define it. Used several times in devices.c. Do not remove!
AC_TYPE_SIZE_T

dnl kstat_open is in kstat on BSD
AC_CHECK_LIB(kstat, kstat_open, SOLLIBS="-lkstat -lsocket", SOLLIBS="")
AC_SUBST(SOLLIBS)

dnl configure the panel plugin
XDT_CHECK_PACKAGE([LIBXFCE4UI], [libxfce4ui-2], [4.12.0])
XDT_CHECK_PACKAGE([LIBXFCE4PANEL], [libxfce4panel-2.0], [4.12.0])

dnl Check for i18n support
XDT_I18N([@LINGUAS@])

dnl Check for debugging support
XDT_FEATURE_DEBUG([mount_debug_default])

dnl Set PACKAGE PREFIX
if test "x${prefix}" = "xNONE"; then
  packageprefix=${ac_default_prefix}
else
  packageprefix=${prefix}
fi

dnl Set PACKAGE DATA & DOC DIR
packagedatadir=share

dnl Subst PACKAGE_DATA_DIR.
NO_PREFIX_PACKAGE_DATA_DIR="${packagedatadir}"
AC_SUBST(NO_PREFIX_PACKAGE_DATA_DIR)
PACKAGE_DATA_DIR="${packageprefix}/${packagedatadir}"
AC_SUBST(PACKAGE_DATA_DIR)

AC_DEFINE_UNQUOTED([PACKAGE_DATA_DIR], "${packageprefix}/${packagedatadir}", ["Data dir"])

AC_CONFIG_FILES([
Makefile
panel-plugin/Makefile
icons/Makefile
po/Makefile.in
])

AC_OUTPUT

